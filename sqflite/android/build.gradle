group 'com.tekartik.sqflite'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.tekartik.sqflite'
    }
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDirs += 'src/main/jniLibs'
            java.srcDirs += 'src/main/java'
            jni.srcDirs = []
        }
    }

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    // https://jitpack.io/#trucnguyenlam/sqlite-android, tag is maintained in develop branch
     implementation 'com.github.trucnguyenlam:sqlite-android:3.45.1'
    
    // for mavenLocal()
    // implementation 'io.requery:sqlite-android:3.45.0-SNAPSHOT'

    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'
    implementation 'androidx.sqlite:sqlite:2.3.1'
    implementation 'androidx.core:core:1.10.1'
//    test dependencies
    testImplementation 'junit:junit:4.13.2'
}
